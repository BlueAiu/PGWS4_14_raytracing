#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/UnityInput.hlsl"

RaytracingAccelerationStructure SceneAS;
RWTexture2D<float4> RenderTarget;

// Uncomment this pragma for debugging the HLSL code in PIX. GPU performance will be impacted.
//#pragma enable_ray_tracing_shader_debug_symbols

#pragma max_recursion_depth 2

struct RayPayload
{
    bool hit;
    float3 radiance;
};

RayDesc generateCameraRay(float2 pixel)
{
    float4x4 view = unity_WorldToCamera;
    float4x4 proj = unity_CameraProjection;

    RayDesc ray;
    ray.Origin = _WorldSpaceCameraPos;
    ray.TMin = 0.f;
    ray.TMax = FLT_MAX;

    float aspect = proj[1][1] / proj[0][0];
    float tanHalfFovY = 1.0f / proj[1][1];

    ray.Direction = normalize(
        (pixel.x * view[0].xyz * tanHalfFovY * aspect) -
        (pixel.y * view[1].xyz * tanHalfFovY) + view[2].xyz);

    return ray;
}

[shader("raygeneration")]
void MyRaygenShader()
{
    uint2 dispatchIdx = DispatchRaysIndex().xy;
    float2 clipPixel = (dispatchIdx + 0.5) / (float2)DispatchRaysDimensions();
    clipPixel = clipPixel * 2.0 - 1.0;
    clipPixel.y = -clipPixel.y;
    RayDesc ray = generateCameraRay(clipPixel);

    RayPayload payload = (RayPayload)0;
    TraceRay(SceneAS, 0, 0xFF, 0, 1, 0, ray, payload);
    RenderTarget[dispatchIdx] = float4(payload.radiance, 1.0);
    // RenderTarget[dispatchIdx] = float4(dispatchIdx.x & dispatchIdx.y, (dispatchIdx.x & 15)/15.0, (dispatchIdx.y & 15)/15.0, 0.0);
}

[shader("miss")]
void Miss00_MyMissShader(inout RayPayload payload : SV_RayPayload)
{
    payload.radiance = float3(0.7, 0.9, 1.0);
    payload.hit = false;
}

struct ShadowPayload
{
    bool hit;
};

[shader("miss")]
void Miss01_ShadowMissShader(inout ShadowPayload payload : SV_RayPayload)
{
    payload.hit = false;
}